buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.17'
    }
}

apply plugin: 'com.google.protobuf'

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenLocal()
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenCentral()
}

dependencies {

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation "io.grpc:grpc-netty:${rootProject.ext.grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${rootProject.ext.grpcVersion}"
    implementation "io.grpc:grpc-stub:${rootProject.ext.grpcVersion}"

    implementation "com.google.protobuf:protobuf-java-util:${rootProject.ext.protocVersion}"
    implementation "com.google.protobuf:protobuf-java:${rootProject.ext.protocVersion}"

    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        implementation 'jakarta.annotation:jakarta.annotation-api'
    }

}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${rootProject.ext.protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${rootProject.ext.grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

/* protoGen 폴더 초기화 */
task cleanProtoGen {
    doFirst {
        delete("$projectDir/src/main/protoGen")
    }
}
clean.dependsOn cleanProtoGen

/* protoGen 폴더 proto 관련 파일 동기화 */
task syncProtoGen(type: Sync) {
    from "$buildDir/generated/source/proto/main"
    into "$projectDir/src/main/protoGen"
}

/* 변경시 jar 빌드 필요 - 자동으로 jar 생성되게 만들어야할 필요 있음 */
jar {
    enabled = true
}

bootJar {
    enabled = false
}

/*
idea {
    module {
        sourceDirs += file("$projectDir/src/generated/main/java")
        sourceDirs += file("$projectDir/src/generated/main/grpc")
        generatedSourceDirs += file("$projectDir/src/generated/main/java")
        generatedSourceDirs += file("$projectDir/src/generated/main/grpc")
    }
}
*/

tasks.register("prepareKotlinBuildScriptModel"){}
